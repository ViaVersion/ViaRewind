plugins {
    id "java-library"
    id "maven-publish"
}

repositories {
    maven {
        url = uri("https://repo.viaversion.com")
    }
    maven {
        url = uri("https://repo.maven.apache.org/maven2/")
    }
    maven {
        url = uri("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    }
    maven {
        url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
    maven {
        url = uri("https://maven.fabricmc.net/")
    }
    maven {
        url = uri("https://repo.spongepowered.org/repository/maven-public/")
    }
    maven {
        url = uri("https://nexus.velocitypowered.com/repository/maven-public/")
    }
}

dependencies {
    compileOnly "com.viaversion:viaversion:${project.viaversion_version}"
    compileOnly "com.viaversion:viabackwards:${project.viabackwards_version}"
    compileOnly "io.netty:netty-all:${project.netty_version}"
    compileOnly "com.google.guava:guava:${project.guava_version}"
}

base {
    archivesName = project.maven_name
    group = project.maven_group
    version = project.maven_version
    description = "ViaBackwards addon to allow 1.8.x and 1.7.x clients on newer server versions."
}

java.sourceCompatibility = JavaVersion.VERSION_17

tasks {
    // Variable replacements
    processResources {
        for (final def file in ["plugin.yml", "fabric.mod.json"]) {
            filesMatching(file) {
                expand(
                        "version": project.version,
                        "description": project.description
                )
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = "Via"
            url = "https://repo.viaversion.com/"

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        main(MavenPublication) {
            artifactId = project.maven_name
            groupId = project.maven_group
            version = project.maven_version

            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}
