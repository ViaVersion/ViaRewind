plugins {
    id "vr.base-conventions"

    id "net.raphimc.class-token-replacer" version "1.0.0"
}

dependencies {
    compileOnly "io.netty:netty-all:4.0.20.Final"
    compileOnly "com.google.guava:guava:17.0"
}

sourceSets {
    main {
        classTokenReplacer {
            property("\${version}", rootProject.maven_version)
            property("\${impl_version}", "git-ViaRewind-${rootProject.maven_version}:${rootProject.latestCommitHash().get()}")
        }
    }
}

// Task to quickly test/debug code changes using https://github.com/ViaVersion/ViaProxy
// For further instructions see the ViaProxy repository README
tasks.register("runViaProxy", JavaExec) {
    dependsOn tasks.jar

    def viaProxyConfiguration = configurations.create("viaProxy")
    viaProxyConfiguration.dependencies.add(dependencies.create("net.raphimc:ViaProxy:3.4.0-SNAPSHOT") {
        transitive = false
    })

    mainClass = "net.raphimc.viaproxy.ViaProxy"
    classpath = viaProxyConfiguration
    workingDir = file("run")
    jvmArgs = ["-DskipUpdateCheck"]
    if (System.getProperty("viaproxy.gui.autoStart") != null) {
        jvmArgs("-Dviaproxy.gui.autoStart")
    }

    doFirst {
        def jarsDir = file("$workingDir/jars")
        jarsDir.mkdirs()
        file("$jarsDir/${project.name}.jar").bytes = tasks.jar.archiveFile.get().asFile.bytes
    }

    doLast {
        file("$workingDir/jars/${project.name}.jar").delete()
        file("$workingDir/logs").deleteDir()
    }
}